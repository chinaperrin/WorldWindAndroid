/*
 * Copyright (c) 2016 United States Government as represented by the Administrator of the
 * National Aeronautics and Space Administration. All Rights Reserved.
 */

precision mediump float;

uniform bool enablePickMode;
uniform bool enableTexture;
uniform vec4 color;
uniform sampler2D texSampler;

varying vec2 texCoord[2];

void main() {
    /* Using the second texture coordinate, compute a mask that's 1.0 when the fragment is inside the surface tile, and
       0.0 otherwise. */
    float sMask = step(0.0, texCoord[1].s) * (1.0 - step(1.0, texCoord[1].s));
    float tMask = step(0.0, texCoord[1].t) * (1.0 - step(1.0, texCoord[1].t));
    float tileMask = sMask * tMask;

    if (enablePickMode && enableTexture) {
        /* Using the first texture coordinate, modulate the RGBA color with the 2D texture's Alpha component (rounded to
           0.0 or 1.0). Finally, modulate the result by the tile mask to suppress fragments outside the surface tile. */
        float texMask = floor(texture2D(texSampler, texCoord[0]).a + 0.5);
        gl_FragColor = color * texMask * tileMask;
    } else if (!enablePickMode && enableTexture) {
        /* Using the first texture coordinate, modulate the RGBA color with the 2D texture's RGBA color. Finally,
           modulate by the tile mask to suppress fragments outside the surface tile. */
        gl_FragColor = color * texture2D(texSampler, texCoord[0]) * tileMask;
    } else {
        /* Modulate the RGBA color by the tile mask to suppress fragments outside the surface tile. */
        gl_FragColor = color * tileMask;
    }
}